plugins {
	id "idea"
	id "maven-publish"
	id "net.minecraftforge.gradle" version "${ForgeGradle}"
	id "org.spongepowered.mixin" version "${SpongePoweredMixin}"
	id "com.github.joschi.licenser" version "${Licenser}"
	id "io.github.themrmilchmann.curseforge-publish" version "${CurseForge}"
}

project.ext.username = System.getenv("MAVEN_USERNAME")
project.ext.password = System.getenv("MAVEN_PASSWORD")
project.ext.token = System.getenv("CURSEFORGE_TOKEN")

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
	mappings channel: "official", version: MinecraftVersion
	reobf = false
	copyIdeResources = true
	accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
	
	runs {
		configureEach {
			workingDirectory project.file("run")
			
			property "forge.logging.markers", "REGISTRIES"
			property "forge.logging.console.level", "debug"
			property "forge.enabledGameTestNamespaces", "xores"
		}
		
		client {}
		
		server {}
		
		gameTestServer {}
		
		data {
			property "xores.data.include", "mod"
			
			args "--mod", "xores", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")
		}
		
		dataPackRarer {
			parent runs.data
			
			property "xores.data.include", "rarer"
		}
		
		dataPackVeryRare {
			parent runs.data
			
			property "xores.data.include", "very_rare"
		}
	}
}

mixin {
	config "xores.mixins.json"
	debug.export = true
}

sourceSets.main.resources {srcDir "src/generated/resources"}

dependencies {
	minecraft "net.minecraftforge:forge:${MinecraftVersion}-${ForgeVersion}"
	
	implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
	
	annotationProcessor "org.spongepowered:mixin:0.8.7:processor"
}

compileJava.dependsOn(updateLicenses)

license {
	header = file("header.txt")
	include("**/*.java")
}

java {
	withSourcesJar()
}

artifacts {
	archives sourcesJar
}

def ver = "${MinecraftVersion}-${ModVersion}"
curseforge {
	apiToken = "${project.ext.token}"
	publications {
		register("curseForge") {
			projectId = "584702"
			
			artifacts.register("main") {
				displayName = "XOres-${ver}"
				from(tasks.named("jar"))
			}
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "net.luis"
			artifactId = "XOres"
			version = ver
			artifact jar
			artifact sourcesJar
		}
	}
	repositories {
		if (project.ext.username != null && project.ext.password != null) {
			maven {
				url "https://maven.luis-st.net/forge/"
				credentials.username = username
				credentials.password = password
			}
		} else {
			System.err.println("No credentials provided. Publishing to maven.luis-st.net not possible.")
		}
	}
}

def resourceTargets = ["META-INF/mods.toml"]
def replaceProperties = [
	MinecraftVersion: MinecraftVersion, MinecraftVersionRange: MinecraftVersionRange,
	ForgeVersion: ForgeVersion, ForgeVersionRange: ForgeVersionRange,
	ForgeLoaderRange: ForgeLoaderRange, ModVersion: ModVersion
]
processResources {
	inputs.properties replaceProperties
	replaceProperties.put "project", project
	
	filesMatching(resourceTargets) {
		expand replaceProperties
	}
}

jar {
	archiveFileName = "XOres-${ver}.jar"
	manifest {
		attributes([
			"Specification-Title": "XOres",
			"Specification-Version": "${ModVersion}",
			"Implementation-Title": project.name,
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd HH:mm:ssZ"),
			"Mod-Author": "Luis-st",
			"Mod-ID": "xores",
			"MixinConfigs": "mixins.xores.json",
		])
	}
}

sourcesJar {
	outputs.upToDateWhen {false}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

sourceSets.each {
	def dir = layout.buildDirectory.dir("sourcesSets/${it.name}")
	it.output.resourcesDir = dir
	it.java.destinationDirectory = dir
}
